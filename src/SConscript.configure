Import('env')

# Define Custom configure checks ---------------------------------------------

def CheckBoostVersion(context, version):
    '''Check if Boost Library is at least of specified version
    '''
    # Boost versions are in format major.minor.subminor
    v_arr = map(int, version.split("."))
    version_n = sum([(v * n) for v, n in zip(v_arr, (1e5, 1e2, 1))])
    context.Message('Checking for Boost version >= %s... ' % (version))
    rv = context.TryCompile('\n'.join([
        '#include <boost/version.hpp>',
        '#if BOOST_VERSION < %d',
        '#error Installed boost is too old!',
        '#endif',
        'int main() { return 0; }',
        '', ]) % version_n, '.cpp')
    context.Result(rv)
    return rv

# Helper functions -----------------------------------------------------------

boostlibtags = ['-mt', '']
def configure_boost_library(libname):
    '''Add a boost library to the configured environment allowing for any
    of the boostlibtags name extensions.

    libname  -- boost library name without any extension

    Note: CheckLib function automatically adds library to the environment.
    '''
    # using global conf defined below
    for t in boostlibtags:
        libnamefull = libname + t
        if conf.CheckLib(libnamefull, language='C++'):
            boostlibtags[:] = [t]
            return
    # library not found here
    print('This program requires %r library' % libname)
    Exit(1)

# Start configuration --------------------------------------------------------

conf = Configure(env, custom_tests={
    'CheckBoostVersion' : CheckBoostVersion,
    })

# boost_python
configure_boost_library('boost_python')

# check for ObjCryst, autoadd appends to LIBS if found.
conf.env['has_objcryst'] = (conf.env['enable_objcryst'] and
    conf.CheckLibWithHeader('ObjCryst', 'ObjCryst/ObjCryst/Crystal.h',
        language='C++', autoadd=True))

env = conf.Finish()

# vim: ft=python
